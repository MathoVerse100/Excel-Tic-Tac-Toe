Sub PlayTicTacToe()
    Call NewTicTacToeSheet
    
    Dim gameBoard(1 To 3, 1 To 3) As String
    Dim turnCounter As Integer
    Dim position As String
    Dim position1 As Integer
    Dim position2 As Integer
    Dim cellName As String
    
    gameBoard(1, 1) = "1"
    gameBoard(1, 2) = "2"
    gameBoard(1, 3) = "3"
    gameBoard(2, 1) = "4"
    gameBoard(2, 2) = "5"
    gameBoard(2, 3) = "6"
    gameBoard(3, 1) = "7"
    gameBoard(3, 2) = "8"
    gameBoard(3, 3) = "9"
    
    turnCounter = 1
    
    Do Until (TicTacToeGameState(gameBoard) Or turnCounter = 10)
        position = InputBox("Your Move", "Player X" & NumberToTicTacToeSymbol(turnCounter), "")
        position1 = CInt(Mid(position, 1, 1))
        position2 = CInt(Mid(position, 2, 1))
        cellName = TicTacToePositionToCellPosition(position1, position2)
        Call DrawTicTacToeXorO(cellName, turnCounter)
        gameBoard(position1, position2) = NumberToTicTacToeSymbol(turnCounter)
        turnCounter = turnCounter + 1
    Loop
    
    If TicTacToeGameState(gameBoard) Then
        If turnCounter Mod 2 = 1 Then
            MsgBox "Player O Won!"
        Else
            MsgBox "Player X Won!"
        End If
    Else
        MsgBox "Draw!"
    End If
End Sub

Sub NewTicTacToeSheet()
    Dim newSheet As Worksheet
    Set newSheet = Sheets.Add(After:=ActiveSheet)
    
    Dim searchResult As Boolean
    Dim i As Integer
    i = 1
    searchResult = False

    Do While i <= Sheets.Count
        If Sheets(i).Name = "Tic-Tac-Toe" Then
            searchResult = True
            Exit Do
        End If
        i = i + 1
    Loop
    
    If searchResult = True Then
        Sheets(i).Delete
    End If
    
    newSheet.Name = "Tic-Tac-Toe"
    DrawTicTacToeBoard newSheet
    newSheet.Range("A1").Select
End Sub

Sub DrawTicTacToeBoard(gameSheet As Worksheet)
    gameSheet.Cells.Delete
    gameSheet.Cells.Select
    Selection.ColumnWidth = 2.5
    
    Range("A1:BF1").Interior.ThemeColor = xlThemeColorLight1
    Range("A2:A67").Interior.ThemeColor = xlThemeColorLight1
    Range("A67:BF67").Interior.ThemeColor = xlThemeColorLight1
    Range("BF2:BF67").Interior.ThemeColor = xlThemeColorLight1
    Range("T2:T66").Interior.ThemeColor = xlThemeColorLight1
    Range("AM2:AM66").Interior.ThemeColor = xlThemeColorLight1
    Range("B23:BE23").Interior.ThemeColor = xlThemeColorLight1
    Range("B45:BE45").Interior.ThemeColor = xlThemeColorLight1

    ActiveWindow.Zoom = 30
End Sub

Sub DrawTicTacToeXorO(symbol As String, turn As Integer)
    If turn Mod 2 = 1 Then
        Call DrawTicTacToeX(Range(symbol))
    Else
        Call DrawTicTacToeO(Range(symbol))
    End If
End Sub

Sub DrawTicTacToeX(position As Range)
    Dim emptyBlocks As Range
    Dim filledBlocks As Range
    
    Set emptyBlocks = position.Range( _
        "A1:R5,A6:D21,E17:R21,O6:R16,F6:M6,G7:L7,H8:K8,I9:J9,E8:E14,F9:F13,G10:G12,H11,N8:N14,M9:M13,L10:L12,K11,I13:J13,H14:K14,G15:L15,F16:M16" _
    )
    Set filledBlocks = position.Range( _
        "E6,E7:F7,F8:G8,G9:H9,H10:K10,K9:L9,L8:M8,M7:N7,N6,I11:J11,E16,E15:F15,F14:G14,G13:H13,H12:K12,K13:L13,L14:M14,M15:N15,N16" _
    )
    
    emptyBlocks.Interior.ThemeColor = xlNone
    filledBlocks.Interior.ThemeColor = xlThemeColorLight1
End Sub

Sub DrawTicTacToeO(position As Range)
    Dim emptyBlocks As Range
    Dim filledBlocks As Range
    
    Set emptyBlocks = position.Range( _
        "A1:R5,A6:D21,E17:R21,P6:R16,E6:G6,E7:F7,E8,M6:O6,N7:O7,O8,E14:E16,F15:F16,G16,M16:O16,N15:O15,O14" _
    )
    Set filledBlocks = position.Range( _
        "H6:L6,G7,M7,F8:G8,M8:N8,E9:E13,O9:O13,F14:G14,M14:N14,G15,M15,H16:L16" _
    )
    
    emptyBlocks.Interior.ThemeColor = xlNone
    filledBlocks.Interior.ThemeColor = xlThemeColorLight1
End Sub

Function TicTacToePositionToCellPosition(row As Integer, column As Integer)
    Dim columnToLetter(1 To 3) As Variant
    columnToLetter(1) = "B"
    columnToLetter(2) = "U"
    columnToLetter(3) = "AN"
    
    TicTacToePositionToCellPosition = columnToLetter(column) & (2 + 22 * (row - 1))
End Function

Function TicTacToeGameState(gameBoard As Variant)
    If (gameBoard(1, 1) = gameBoard(1, 2) And gameBoard(1, 2) = gameBoard(1, 3)) _
        Or _
       (gameBoard(2, 1) = gameBoard(2, 2) And gameBoard(2, 2) = gameBoard(2, 3)) _
        Or _
       (gameBoard(3, 1) = gameBoard(3, 2) And gameBoard(3, 2) = gameBoard(3, 3)) _
        Or _
       (gameBoard(1, 1) = gameBoard(2, 1) And gameBoard(2, 1) = gameBoard(3, 1)) _
        Or _
       (gameBoard(1, 2) = gameBoard(2, 2) And gameBoard(2, 2) = gameBoard(3, 2)) _
        Or _
       (gameBoard(1, 3) = gameBoard(2, 3) And gameBoard(2, 3) = gameBoard(3, 3)) _
        Or _
       (gameBoard(1, 1) = gameBoard(2, 2) And gameBoard(2, 2) = gameBoard(3, 3)) _
        Or _
       (gameBoard(1, 3) = gameBoard(2, 2) And gameBoard(2, 2) = gameBoard(3, 1)) Then
        
        TicTacToeGameState = True
    Else
        TicTacToeGameState = False
    End If
End Function

Function TicTacToeDraw(gameBoard As Variant)
    If (gameBoard(1, 1) = "X" Or gameBoard(1, 1) = "O") And _
       (gameBoard(1, 2) = "X" Or gameBoard(1, 2) = "O") And _
       (gameBoard(1, 3) = "X" Or gameBoard(1, 3) = "O") And _
       (gameBoard(2, 1) = "X" Or gameBoard(2, 1) = "O") And _
       (gameBoard(2, 2) = "X" Or gameBoard(2, 2) = "O") And _
       (gameBoard(2, 3) = "X" Or gameBoard(2, 3) = "O") And _
       (gameBoard(3, 1) = "X" Or gameBoard(3, 1) = "O") And _
       (gameBoard(3, 2) = "X" Or gameBoard(3, 2) = "O") And _
       (gameBoard(3, 3) = "X" Or gameBoard(3, 3) = "O") Then
       
        TicTacToeDraw = True
    Else
        TicTacToeDraw = False
    End If
End Function

Function ReverseTicTacToePlayer(symbol As String)
    If symbol = "X" Then
        ReverseTicTacToePlayer = "O"
    ElseIf symbol = "O" Then
        ReverseTicTacToePlayer = "X"
    Else
        ReverseTicTacToePlayer = "Invalid Symbol"
    End If
End Function

Function NumberToTicTacToeSymbol(value As Integer)
    If value Mod 2 = 1 Then
        NumberToTicTacToeSymbol = "X"
    Else
        NumberToTicTacToeSymbol = "O"
    End If
End Function
